plugins {
	id 'org.springframework.boot' version '2.1.12.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'java'
	id 'java-library'
	id 'maven-publish'
}

group = 'com.ggresillion.spring'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

bootJar {
	enabled = false
}

jar {
	enabled = true
}

test {
	useJUnitPlatform()
}

task sourceJar(type: Jar) {
	classifier "sources"
	from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier "javadoc"
	from javadoc.destinationDir
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			pom {
				name = 'Spring Media Manager'
				description = 'Library to ease the use of medias within Spring projects'
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
			}
			groupId group
			artifactId rootProject.name
			version version

			from components.java

			artifact(sourceJar) {
				classifier = 'sources'
			}
			artifact(javadocJar) {
				classifier = 'javadoc'
			}
		}
	}
//	repositories {
//		maven {
//			url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
//			credentials {
//				username sonatypeUsername
//				password sonatypePassword
//			}
//		}
//	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	api 'org.imgscalr:imgscalr-lib:4.2'
	api 'io.minio:minio:6.0.11'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.postgresql:postgresql'
}
